;-------------------------------------------------
;「노예시장」의 명칭
;-------------------------------------------------
@SHOP_SLG_NAME64
RESULTS:0 '= "노예시장"

;-------------------------------------------------
;「노예시장」의 선택 가부
;-------------------------------------------------
@SHOP_SLG_CHECK64
SIF !IS_SLAVEMARKET_AVAILABLE()
	RETURN 0
SIF CFLAG:MASTER:소속 == 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;「노예시장」의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_SLG_EVENTBUY64
IF IS_SP_COUNTRY(CFLAG:MASTER:소속)
	CALL SLAVEMARKET_SELL_CHARA()
ELSE
	CALL SLAVEMARKET
ENDIF
RETURN 1

;-------------------------------------------------
;「노예시장」본체
;-------------------------------------------------
@SLAVEMARKET
#DIM 대상
#DIM 대상첨자
#DIM FIRST_LINE

FIRST_LINE = LINECOUNT

CALL SINGLE_DRAWLINE
PRINTFORML 「～날마다 오지 않는 기회! 품질 좋은 상품이 들어와 있습니다!」
PRINTFORML %조사처리(ANAME(MASTER),"는")% 노예시장을 찾아왔다
PRINTFORML 다 헤진 옷을 입은 여자들이 철창에 갇힌 채 진열되어 있다
PRINTFORML 직원처럼 보이는 로봇이 다가와, 판매중인 노예의 리스트를 건네주었다……
PRINTFORML 소지금:{MONEY}
CALL SINGLE_DRAWLINE

VARSET SHOP_CHARA_LIST, -1
FOR LOCAL, 0, VARSIZE("SLAVEMARKET_GOODS")
	SHOP_CHARA_LIST:LOCAL = ID_TO_CHARA(SLAVEMARKET_GOODS:LOCAL)
NEXT

FOR LOCAL, 0, VARSIZE("SLAVEMARKET_GOODS")
	LOCAL:1 = ID_TO_CHARA(SLAVEMARKET_GOODS:LOCAL)
	SIF LOCAL:1 == -1
		CONTINUE
	;인질 해방 요구 따위에 의해 이미 포로가 아니게 되고 있는 경우, 퍼지
	IF !IS_SP_COUNTRY(CFLAG:(LOCAL:1):포로처) && !IS_SP_COUNTRY(CFLAG:(LOCAL:1):소속)
		SLAVEMARKET_GOODS:LOCAL = 0
		CONTINUE
	ENDIF
	CALL SLAVEMARKET_SHOW_INFO(LOCAL:1, LOCAL)
	CALL SINGLE_DRAWLINE
NEXT

PRINTL 
PRINTFORML [98] 매각
PRINTFORML [99] 돌아온다

$INPUT_LOOP
INPUT

IF RESULT == 99
	RETURN 0
ELSEIF RESULT == 98
	CALL SLAVEMARKET_SELL_CHARA
	CLEARLINE FIRST_LINE - LINECOUNT
	RESTART
ENDIF

IF RESULT < 0
	CLEARLINE 1
	GOTO INPUT_LOOP
ELSEIF 100 <= RESULT && RESULT < VARSIZE("SLAVEMARKET_GOODS") + 100
	대상첨자 = RESULT - 100
	대상 = ID_TO_CHARA(SLAVEMARKET_GOODS:대상첨자)
	IF 대상 == -1
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	CALL SINGLE_DRAWLINE
	CALL SHOW_INFO_WITH_UI(대상, 1)
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ELSEIF VARSIZE("SLAVEMARKET_GOODS") <= RESULT || MONEY < SLAVEMARKET_PRICE:RESULT
		CLEARLINE 1
		GOTO INPUT_LOOP
ENDIF

대상 = ID_TO_CHARA(SLAVEMARKET_GOODS:RESULT)
대상첨자 = RESULT


IF 대상 == -1
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

대상 = ID_TO_CHARA(SLAVEMARKET_GOODS:RESULT)
대상첨자 = RESULT
PRINTFORML 「헤헤, 그 녀석은 %조사처리(NAME_FORMAL(대상),"라")%는 노예인데 말입죠」
PRINTFORML 「지금이라면 손님 얼굴을 봐서 {SLAVEMARKET_PRICE:대상첨자} 정도만 받아드립죠, 예」
SIF 대상첨자 == SLAVEMARKET_SPECIALGOODS
	PRINTFORML 「살짝 "특이사항이 있는" 녀석이라, 특별 할인으로 가격도 깎아드립죠」
IF IS_LOVER(대상)
	PRINTFORML 「……예? 비싸다굽쇼?」
	PRINTFORML 「그야 물론, 이 녀석이 당신의 "이것" 이라는 걸 알고 있기 때문 아니겠습니까? 예」
	PRINTFORML 「이 정도도 비싸시다면야, 어쩌면 소중한 여자가 다른 남자에게 팔려버릴지도 모른다굽쇼? 키, 히힉!」
ENDIF
PRINTFORML 「그래서?   어떻게 하실 겁니까?」
PRINTL
CALL ASK_MULTI_JUDGE(@"%조사처리(ANAME(대상),"를")% 산다", MONEY >= SLAVEMARKET_PRICE:대상첨자, "그만둔다", 1)
IF RESULT == 1
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF

MONEY -= SLAVEMARKET_PRICE:대상첨자
PRINTFORMW 「히히, 그렇다면 협상 성립입니다」
PRINTFORMW 「자, 가져가시지요」

;확률로 에로이 밴트  특매라면 확정
IF IS_FEMALE(대상) && (20 > RAND:100 || 대상첨자 == SLAVEMARKET_SPECIALGOODS)
	PRINTFORML
	PRINTFORMW ……남자가 데려 온 %ANAME(대상)%의 모습을 보고, %조사처리(ANAME(MASTER),"는")% 눈을 의심했다
	PRINTFORML 실오라기 하나 걸치지 못한 %조사처리(ANAME(대상),"는")% 눈가리개를 쓴 채 손발에는 족쇄를 차고 있다
	PRINTFORMW 노출된 몸에는, 음란한 피어스가 박혀 있다
	PRINTFORML 전신이 백탁 투성이에, 구멍에서도 끈적거리는 액체가 질척질척 흘러내리고 있다
	PRINTFORMW 여기저기에 쓰여진 바를 정 자가, %조사처리(ANAME(대상),"가")% 범해졌음을 말해주고 있었다
	PRINTFORMW 굉장히 비참한 모습이었지만, %조사처리(ANAME(대상),"는")% 헤실헤실 웃고 있다
	PRINTFORMW ……이것은 즐거워서 웃고 있는 것이 아니다. 뭔가 이상한 약의 작용이다……
	PRINTFORML
	PRINTFORML 「아～, 시장에 진열해 두고 있는 동안에는, 제 처지를 꺠닫도록 노예로서 부하들이 사용하도록 해서 말이죠」
	PRINTFORML 「세척은 셀프입니다 손님. 죄송합니다만, 예」
	PRINTFORMW 「아차차, 저희는 반품은 받지 않습니다. 그게 이 바닥 문화라 말입죠, 예」
	SIF 대상 == SLAVEMARKET_SPECIALGOODS
		PRINTFORMW 「뭐, 특별 할인에는 그만한 이유가 있다는 겁니다, 헤헤헤」
	IF IS_LOVER(대상)
		PRINTFORMW 「딱히 상관없지 않습니까～?   연인과 뗄레야 뗼 수 없는 사이가 된 것 아닙니까?」
		PRINTFORMW 「혹시 겨우 이걸로 정이 떨어지셨습니까? 연인 사이라면 좀 더 소중히 보듬어 주셔야지요!」
	ENDIF
	PRINTFORMW 당장 눈앞의 노예상을 쏴버리고 싶지만, 이곳에는 경비병들이 빼곡히 깔려 있다. 날뛰면 내가 위험하다
	PRINTFORMW 이를 악물고, %조사처리(ANAME(MASTER),"는")% %조사처리(ANAME(대상),"를")% 받았다……
	CALL FUCK_GANGBANG(대상, GET_SPERM_ID("노예 상인"), @"노예 상인의 \@RAND:2 ? 페니스 # 입술\@", "노예 상인")
	CFLAG:대상:약물의존 += RAND(30, 50)
	CALL SET_PIERCE_RANDOM(대상, 0)
ENDIF

IF IS_LOVER(대상)
	PRINTFORMW 「키히히, 이 정도로 정이 떨어지셨습니까? 연인 사이라면 좀 더 소중히 보듬어 주셔야지요! 캬하하!」
ELSE
	PRINTFORML 「엣헤헤헤, 매번 감사합니다」
	PRINTFORMW 「그 장난감과 행복하십쇼! 예」
ENDIF



PRINTFORMW %조사처리(ANAME(대상),"를")% 받아들고, 노예시장을 뒤로 했다……
;별 세력에 소속（특수 세력 이외）
IF IS_COUNTRY(CFLAG:대상:소속) && !IS_SP_COUNTRY(CFLAG:대상:소속) && CFLAG:대상:소속 != CFLAG:MASTER:소속
	PRINTFORMW ……%조사처리(ANAME(대상),"는")% %ANAME(GET_COUNTRY_BOSS(CFLAG:대상:소속))% 소속이다
	PRINTFORMW 돌려주는 것이 인간의 도리인 것 같다만……
	PRINTFORML 어떻게 합니까?
	CALL SINGLE_DRAWLINE
	CALL ASK_MULTI("돌려준다", "부원으로 받아들인다", "노예로서 감금한다")
	IF RESULT == 0
		PRINTFORMW %조사처리(ANAME(대상),"를")% %ANAME(GET_COUNTRY_BOSS(CFLAG:대상:소속))%에게 돌려주기로 했다
		PRINTFORMW %ANAME(GET_COUNTRY_BOSS(CFLAG:대상:소속))%에게 깊게 감사받았다……
		CALL CHANGE_RELATION_C_TO_C(CFLAG:대상:소속, CFLAG:MASTER:소속, 600, -600)
		CFLAG:대상:호감도 = MAX(500, CFLAG:대상:호감도 + 500)
		CFLAG:대상:종속도 = MAX(500, CFLAG:대상:종속도 + 500)
		CFLAG:대상:의존도 = MAX(100, CFLAG:대상:의존도 + 100)
		CALL CAPTURE(대상, 0)
	ELSEIF RESULT == 1
		PRINTFORMW %ANAME(GET_COUNTRY_BOSS(CFLAG:대상:소속))%에게 내가 산 것을 공짜로 돌려줄 정도의 의리는 없다
		PRINTFORMW 간부 대우를 해 주는 조건으로, %ANAME(대상)%에게 협력을 약속받았다……
		CALL CHANGE_COUNTRY(대상, CFLAG:MASTER:소속, 1)
		CFLAG:대상:호감도 = MAX(0, CFLAG:대상:호감도 + 500)
		CFLAG:대상:종속도 = MAX(0, CFLAG:대상:종속도 + 500)
		CFLAG:대상:의존도 = MAX(0, CFLAG:대상:의존도 + 100)
	ELSE
		PRINTFORMW 아니, 돈을 내고 샀으니까, 이제 %조사처리(ANAME(대상),"는")% 내 것이다
		PRINTFORMW 어떻게 사용해 줄까 생각하면서, %조사처리(ANAME(MASTER),"는")% 시장을 뒤로 했다……
		CALL CHANGE_COUNTRY(LOCAL:0, 0, 1)
		CALL CAPTURE(대상, CFLAG:MASTER:소속)
	ENDIF
;어디에도 소속하지 않은지, 특수 세력 소속
ELSEIF CFLAG:대상:소속 != CFLAG:MASTER:소속
	PRINTFORML ……%ANAME(대상)%의 대우는 어떻게 하지?
	CALL SINGLE_DRAWLINE
	CALL ASK_YN("부원으로 받아들인다", "감금해 둔다")
	IF RESULT == 0
		PRINTFORMW %조사처리(ANAME(대상),"는")% %ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:소속))%의 부원으로 입부하게 되었다……
		CALL CHANGE_COUNTRY(대상, CFLAG:MASTER:소속, 1)
		CFLAG:대상:호감도 = MAX(500, CFLAG:대상:호감도 + 500)
		CFLAG:대상:종속도 = MAX(500, CFLAG:대상:종속도 + 500)
		CFLAG:대상:의존도 = MAX(100, CFLAG:대상:의존도 + 100)
	ELSE
		PRINTFORMW %조사처리(ANAME(대상),"를")% 도망치지 못하도록 감금해 두기로 했다……
		CALL CHANGE_COUNTRY(대상, 0, 1)
		CALL CAPTURE(대상, CFLAG:MASTER:소속)
	ENDIF
;자국의 사관
ELSE
	CFLAG:대상:호감도 = MAX(500, CFLAG:대상:호감도 + 500)
	CFLAG:대상:종속도 = MAX(500, CFLAG:대상:종속도 + 500)
	CFLAG:대상:의존도 = MAX(100, CFLAG:대상:의존도 + 100)
	CALL CHANGE_COUNTRY(대상, CFLAG:MASTER:소속, 1)
ENDIF

;포로 상태로부터 해방, 부대로부터 제외, 산 캐릭터를 배열로부터 삭제
;만약을 위해 방랑 플래그를 꺾는다
CFLAG:대상:특수상태 = 0
CALL FORCE_FREE(대상)
SLAVEMARKET_GOODS:대상첨자 = 0


;-------------------------------------------------
;「노예시장」매각 부분
;-------------------------------------------------
@SLAVEMARKET_SELL_CHARA
#DIM 대상
#DIM 행선지
#DIM FIRST_LINE
#DIM 대상매각액
#DIM 합계액

FIRST_LINE = LINECOUNT
PRINTFORML 「히히, 누구를 파실 겁니까?」
CALL SINGLE_DRAWLINE
CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SEX(-1, "SLAVEMARKET_SELL_CHARA", "SLAVEMARKET_SELL_CHARA")

IF SELECTED_CHARA_NUM == 0
	PRINTFORMW 「이런, 유감이군요, 히힛」
	RETURN 0
ENDIF

;개개의 매각액 표시할까나
;에서도 그건 그걸로 음울할까
합계액 = 0
FOR LOCAL, 0, SELECTED_CHARA_NUM
	대상 = SELECTED_CHARA:LOCAL
	대상매각액 = SLAVEMARKET_CALC_PRICE(대상) / 5
	합계액 += 대상매각액
NEXT
PRINTFORML 담당자는 %조사처리(ANAME(대상, SELECTED_CHARA_NUM),"를")%, 욕망에 찬 눈으로 품평하듯 바라봤다……
PRINTFORML 「흐음…… 한 {합계액} 정도면 어떠십니까?」

CALL ASK_YN("매각", "그만둔다")
IF RESULT == 1
	PRINTFORMW 「쳇, 이제 와서 양심에 찔리기라도 한 겁니까?」
	CLEARLINE FIRST_LINE - LINECOUNT
	RESTART
ENDIF

PRINTFORML 「히힛, 매번 감사합니다!」
PRINTFORML %ANAME(대상, SELECTED_CHARA_NUM)%들은 형벌 도구를 끼울 수 있어 어딘가로 데려가졌다……

MONEY += 합계액

FOR LOCAL, 0, SELECTED_CHARA_NUM
	대상 = SELECTED_CHARA:LOCAL
	WHILE 1
		행선지 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:IFRAND(@"1TO{VARSIZE("SP_COUNTRY_ID") - 1}, D{SP_COUNTRY_ID:특수세력_자경단}", 1))
		SIF GROUPMATCH(행선지, -1, CFLAG:MASTER:소속)
			CONTINUE
		PRINTFORML %조사처리(ANAME(대상),"는")% %ANAME(GET_COUNTRY_BOSS(행선지))% 세력에 팔려 갔다……
		CALL CHANGE_COUNTRY(대상, 0, 1)
		CALL CAPTURE(대상, 행선지)
		CFLAG:대상:호감도 = CFLAG:대상:호감도 / 2 - 1000
		CFLAG:대상:의존도 = CFLAG:대상:의존도 / 2 - 1000
		CFLAG:대상:종속도 = CFLAG:대상:종속도 / 2 - 1000
		CFLAG:대상:지배도 = CFLAG:대상:지배도 / 2 - 1000
		BREAK
	WEND
NEXT

WAIT

@SELECT_CHARA_LIST_SHOW_LOGIC_SLAVEMARKET_SELL_CHARA(대상)
#DIM 대상
RETURN CFLAG:대상:소속 == CFLAG:MASTER:소속 && GET_COUNTRY_BOSS(CFLAG:대상:소속) != 대상 && !IS_SP_CHARA(대상) && !IS_ANIMAL(대상) && (IS_LOVER(대상) || IS_SLAVE(대상) || IS_SLAVED_BY(대상)) && CFLAG:대상:행동불능상태 != 행동불능_아이

@SELECT_CHARA_LIST_SELECT_LOGIC_SLAVEMARKET_SELL_CHARA(대상)
#DIM 대상
RETURN CFLAG:대상:포로처 == 0


;-------------------------------------------------
;노예시장의 상품의 정보 표시＋버튼 생성
;-------------------------------------------------
@SLAVEMARKET_SHOW_INFO(대상, 번호)
#DIM 대상
#DIM 번호
IF MONEY >= SLAVEMARKET_PRICE:번호
	PRINTBUTTON @"%"[" + ANAME(대상) + "]", MAX_CHARANAME_LENGTH + 2, LEFT%", 번호
	PRINTFORM  판매액:{SLAVEMARKET_PRICE:번호, 8, LEFT}
	PRINTBUTTON "[능력표시]", 번호 + 100
	SIF SLAVEMARKET_SPECIALGOODS == 번호
		CALL COLOR_PRINT("특매!", 칼라_핑크)
ELSE
	SETCOLOR 칼라_선택불가
	PRINTPLAINFORM %"[" + ANAME(대상) + "]", MAX_CHARANAME_LENGTH + 2, LEFT%
	RESETCOLOR
	PRINTFORM  판매액:{SLAVEMARKET_PRICE:번호, 8, LEFT}
	SIF SLAVEMARKET_SPECIALGOODS == 번호
		CALL COLOR_PRINT("특별 할인!   ", 칼라_핑크)
	CALL COLOR_PRINT("자금이 부족하므로 구입 불가", 칼라_경고)
ENDIF
PRINTL
PRINT 성지식:
PRINTFORM {ABL:대상:성지식, 2, RIGHT}
PRINT  욕망:
CALL PRINT_ALPHABET_RANK(랭크_그외, ABL:대상:욕망)
PRINTFORM {ABL:대상:욕망, 2, RIGHT}
PRINT  성기술:
CALL PRINT_ALPHABET_RANK(랭크_그외, ABL:대상:성기술)
PRINTFORM {ABL:대상:성기술, 3, RIGHT}
PRINT  봉사:
CALL PRINT_ALPHABET_RANK(랭크_그외, ABL:대상:봉사)
PRINTFORM {ABL:대상:봉사, 3, RIGHT}
PRINT  성교:
CALL PRINT_ALPHABET_RANK(랭크_그외, ABL:대상:성교)
PRINTFORM {ABL:대상:성교, 3, RIGHT}
PRINT  정애:
CALL PRINT_ALPHABET_RANK(랭크_그외, ABL:대상:정애)
PRINTFORM {ABL:대상:정애, 3, RIGHT}
PRINTL
PRINTFORM 소지 소질:
LOCAL:1 = 0
FOR LOCAL, 0, VARSIZE("SEX_TALENT_NAME")
	IF GETBIT(TALENT:대상:음란계, LOCAL)
		PRINTFORM %SEX_TALENT_NAME:LOCAL% 
		LOCAL:1 = 1
	ENDIF
NEXT
FOR LOCAL, 0, VARSIZE("SP_FALLEN_TALENT_NAME")
	IF GETBIT(TALENT:대상:특수세력함락계, LOCAL)
		PRINTFORM %SP_FALLEN_TALENT_NAME:LOCAL% 
		LOCAL:1 = 1
	ENDIF
NEXT
SIF !LOCAL:1
	PRINT 없음
PRINTL

;-------------------------------------------------
;노예시장을 이용할 수 있을까를 돌려주는 함수
;-------------------------------------------------
@IS_SLAVEMARKET_AVAILABLE
#FUNCTION

;특수 세력인 세력이 필요(주인공이 소속하지 않은 곳으로)
FOR LOCAL, 0, MAX_COUNTRY
	SIF IS_SP_COUNTRY(LOCAL) && CFLAG:MASTER:소속 != LOCAL
		RETURNF 1
NEXT

;상품이 있으면 ok
;FOR LOCAL, 0, SLAVEMARKET_MAX_GOODS
;	SIF ID_TO_CHARA(SLAVEMARKET_GOODS:LOCAL) != -1
;		RETURNF 1
;NEXT

RETURNF 0

;-------------------------------------------------
;노예시장용, 노예의 금액 계산 함수
;-------------------------------------------------
@SLAVEMARKET_CALC_PRICE(대상)
#FUNCTION
#DIM 대상
#DIM 이마

;기초치에 능력과 호색가 능력을 가산한다
이마 = MIN(ABL:대상:무투 + ABL:대상:방위 + ABL:대상:지략 + ABL:대상:정치 + ABL:대상:가창 + ABL:대상:요리, 1000) * 30
이마 += ABL:대상:성지식 * 500
이마 += MIN(ABL:대상:욕망 + ABL:대상:성기술 + ABL:대상:봉사 + ABL:대상:성교, 50) * 500
이마 += MIN(ABL:대상:노출 + ABL:대상:정애 + ABL:대상:자위, 10) * 300
이마 += MIN(ABL:대상:Ｃ감 + ABL:대상:Ｖ감 + ABL:대상:Ａ감 + ABL:대상:Ｂ감 + ABL:대상:Ｍ감, 40) * 300
;자국 소속과 특수 세력 함락완료는 높다
SIF CFLAG:대상:소속 == CFLAG:MASTER:소속
	이마 += 3000
FOR LOCAL, 0, MAX_SP_COUNTRY
	SIF GETBIT(TALENT:대상:특수세력함락계, LOCAL)
		이마 += 3000
NEXT

;연인이라고 발 밑 볼 수 있다
SIF IS_LOVER(대상)
	이마 += 10000

RETURNF 이마

;-------------------------------------------------
;노예시장용, 그 턴의 상품의 준비
;-------------------------------------------------
@SLAVEMARKET_PREPARE_GOODS
VARSET SLAVEMARKET_GOODS
VARSET SLAVEMARKET_SPECIALGOODS

CALL SELECT_CHARA_RANDOM_MULTI(SLAVEMARKET_MAX_GOODS, "SLAVEMARKET_PREPARE_GOODS")
;후보가 없으면 없음으로.
SIF SELECTED_CHARA_NUM < 1
	RETURN 0

FOR LOCAL, 0, MIN(SELECTED_CHARA_NUM, SLAVEMARKET_MAX_GOODS)
	SLAVEMARKET_GOODS:LOCAL = GET_ID(SELECTED_CHARA:LOCAL)
	SLAVEMARKET_PRICE:LOCAL = SLAVEMARKET_CALC_PRICE(SELECTED_CHARA:LOCAL)
NEXT

IF !RAND:3
	SLAVEMARKET_SPECIALGOODS = RAND:MIN(SELECTED_CHARA_NUM, SLAVEMARKET_MAX_GOODS)
	SLAVEMARKET_PRICE:SLAVEMARKET_SPECIALGOODS /= 2
ELSE
	SLAVEMARKET_SPECIALGOODS = SLAVEMARKET_MAX_GOODS
ENDIF
RETURN 1

@SELECT_CHARA_RANDOM_LOGIC_SLAVEMARKET_PREPARE_GOODS(대상)
#DIM 대상
;SP캐릭터는 대상으로 하지 않는다（도둑 그 자체가 리스트업 되는 것을 막는다
SIF IS_SP_CHARA(대상)
	RETURN 0
;특수 세력의 포로. 없고 특수 세력 소속으로 포로가 아니다
RETURN IS_SP_COUNTRY(CFLAG:대상:포로처) || (IS_SP_COUNTRY(CFLAG:대상:소속) && !CFLAG:대상:포로처)
