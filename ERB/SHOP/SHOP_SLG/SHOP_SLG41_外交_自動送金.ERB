;-------------------------------------------------
;자동 송금 설정 화면
;-------------------------------------------------
@AUTO_SENDMONEY_SETTING
#DIM FIRST_LINE

FIRST_LINE = LINECOUNT

CALL SINGLE_DRAWLINE
PRINTFORML 세금을 거둬 학원의 예산이 늘어나는 시점에, 자동으로 각 세력과 회담해 예산을 송금합니다
PRINTFORML 예산이 설정된 액수보다 부족할 경우, 가능한 만큼만 송금합니다
PRINTFORML 예산이 없으면 징병과 도시 개발을 지시할 수 없게 되므로, 여유를 두고 송금하는 것이 좋습니다
CALL SINGLE_DRAWLINE

FOR LOCAL, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL) || LOCAL == CFLAG:MASTER:소속 || COUNTRY_IS_CLOSED:LOCAL
		CONTINUE
	PRINTFORML [{LOCAL, 2, LEFT}] %ANAME(GET_COUNTRY_BOSS(LOCAL)), MAX_CHARANAME_LENGTH, LEFT% 송금액:\@ AUTO_INVEST_PRICE:LOCAL > 0 ? {AUTO_INVEST_PRICE:LOCAL} # 없음 \@
NEXT
CALL SINGLE_DRAWLINE
PRINTFORML [98]일괄 설정
PRINTFORML [99]돌아온다

INPUT

IF RESULT == 99
	RETURN
ELSEIF RESULT == 98
	CALL AUTO_SENDMONEY_SETTING_SET_PRICE
	VARSET AUTO_INVEST_PRICE, RESULT
ELSEIF INRANGE(RESULT, 0, MAX_COUNTRY - 1) && IS_COUNTRY(RESULT) && RESULT != CFLAG:MASTER:소속 && !COUNTRY_IS_CLOSED:RESULT
	LOCAL = RESULT
	CALL AUTO_SENDMONEY_SETTING_SET_PRICE
	AUTO_INVEST_PRICE:LOCAL = RESULT
ENDIF
CLEARLINE LINECOUNT - FIRST_LINE
RESTART

;-------------------------------------------------
;자동 송금 가격 설정
;-------------------------------------------------
@AUTO_SENDMONEY_SETTING_SET_PRICE
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
PRINTFORML 얼마로 설정합니까? (1～50000, 0으로 캔슬)
INPUT
IF RESULT < 0 || RESULT > 50000
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF
PRINTFORML {RESULT}로 설정했습니다
RETURN RESULT

;-------------------------------------------------
;자동 송금
;-------------------------------------------------
@AUTO_SENDMONEY
#DIM 송금액

SIF !IS_COUNTRY(CFLAG:MASTER:소속)
	RETURN

LOCAL:1 = 0

FOR LOCAL, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL) || LOCAL == CFLAG:MASTER:소속 || COUNTRY_IS_CLOSED:LOCAL || AUTO_INVEST_PRICE:LOCAL == 0
		CONTINUE
	SIF DONE_DIPLOMACY_FRIENDSHIP:LOCAL
		CONTINUE
	SIF MONEY:(CFLAG:MASTER:소속) <= 0
		BREAK
	IF LOCAL:1 == 0
		CALL SINGLE_DRAWLINE
		LOCAL:1 = 1
	ENDIF
	송금액 = MIN(AUTO_INVEST_PRICE:LOCAL, MONEY:(CFLAG:MASTER:소속))
	CALL SEND_MONEY(LOCAL, 송금액)
	DONE_DIPLOMACY_FRIENDSHIP:LOCAL = 1
	IF MONEY:(CFLAG:MASTER:소속) <= 0
		PRINTFORML (예산이 바닥났으므로, 자동 송금을 종료합니다)
		BREAK
	ENDIF
NEXT

