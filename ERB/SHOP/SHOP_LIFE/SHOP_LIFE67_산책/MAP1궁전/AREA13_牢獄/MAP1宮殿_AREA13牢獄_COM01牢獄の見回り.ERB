;-------------------------------------------------
; 「감옥의 순찰」의 존재 판정
;-------------------------------------------------
@SHOP_LIFE67_MAP1_AREA13_COM1_EXIST
RETURN 1

;-------------------------------------------------
; 「감옥의 순찰」의 명칭
;-------------------------------------------------
@SHOP_LIFE67_MAP1_AREA13_COM1_NAME
RESULTS:0 '= "반성실을 순찰한다"
RETURN 1

;-------------------------------------------------
; 「감옥의 순찰」의 선택 가부
;-------------------------------------------------
@SHOP_LIFE67_MAP1_AREA13_COM1_CHECK(ARG:0)
RETURN 1

;-------------------------------------------------
; 「감옥의 순찰」의 선택 가능 캐릭터 존재 판정
;-------------------------------------------------
@SHOP_LIFE67_MAP1_AREA13_COM1_CHECKCHARA(ARG:0)

SIF MASTER == ARG:0
	RETURN 0
SIF !TALENT:(ARG:0):낙인
	RETURN 0
SIF CFLAG:(ARG:0):행동완료
	RETURN 0
SIF CFLAG:(ARG:0):포로처
	RETURN 0
SIF CFLAG:(ARG:0):소속 != CFLAG:MASTER:소속
	RETURN 0
SIF COOLTIME:(ARG:0):0
	RETURN 0
SIF IS_ANIMAL(ARG:0)
	RETURN 0
SIF CFLAG:(ARG:0):종속도 <= 0
	RETURN 0

RETURN 1

;-------------------------------------------------
; 「감옥의 순찰」의 메인 처리
;-------------------------------------------------
@SHOP_LIFE67_MAP1_AREA13_COM1_MAIN
#DIM L_COUNTER
#DIM 대상

;리셋트
CALL COM_AUTO_RANDOMIZED_WEIGHTED_START

;후보 추가
FOR L_COUNTER, 0, CHARANUM
	RESULT = 0
	CALL SHOP_LIFE67_MAP1_AREA13_COM1_CHECKCHARA(L_COUNTER)
	SIF !RESULT
		CONTINUE
	CALL COM_AUTO_RANDOMIZED_WEIGHTED_VALUE(L_COUNTER, CFLAG:L_COUNTER:종속도)
NEXT

;결과 취득
대상 = COM_AUTO_RANDOMIZED_WEIGHTED_RESULT()

CALL TRAIN_TARGET_SETTING(대상)
VARSET PRISONER_TARGET, -1
PRISONER_TARGET:0 = 대상

FLAG:조교모드 = 조교_포로조교
FLAG:우후후플래그 = 1

CALL START_TRAIN_COMMON

CALL SHOP_LIFE67_MAP1_AREA13_COM1_TEXT(대상)
WAIT

LINES_SHOP = LINECOUNT

RETURN 1

;-------------------------------------------------
; 묘사 1
;-------------------------------------------------
@SHOP_LIFE67_MAP1_AREA13_COM1_TEXT(대상)
#DIM 대상

PRINTFORML  %조사처리(ANAME(MASTER),"가")% 반성실을 돌아보러 갔더니, %조사처리(ANAME(대상),"가")% 반성실 안에서 웅크리고 앉아있는 것을 발견했다.
WAIT
PRINTL 
PRINTFORML  놀라서 %조사처리(ANAME(대상),"를")% 불러 누가 이곳에 밀어넣은 건지 물었다.
PRINTFORML  그러나 %조사처리(ANAME(대상),"는")% 고개를 젓더니 키카드를 보여 주었다.
PRINTFORML  반성실에 갇힌 채 혼자 부끄러운 짓을 하고 있던 것 같다.
WAIT
PRINTL 
PRINTFORML  못보던 사이에 더 떨어질 수 없을 정도로 저속해져 버린 것 같다.
PRINTFORML  이건 『생활 지도』가 필요할 것 같다…

RETURN 0

