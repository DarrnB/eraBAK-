;-------------------------------------------------
;「무기변경」의 명칭
;-------------------------------------------------
@SHOP_LIFE_NAME55
RESULTS:0 '= "장비변경"
;-------------------------------------------------
;「무기변경」의 선택 가부
;-------------------------------------------------
@SHOP_LIFE_CHECK55
SIF CFLAG:MASTER:포로처
	RETURN 0
RETURN 1

;-------------------------------------------------
;「무기변경」의 선택 가능 캐릭터 존재 판정
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA55(ARG:0)
;방랑일 경우 함락 상대만 가능,미등장 사망 캐릭터 불가, 같은소속이고 포로나 동물이 아님
SIF CFLAG:(ARG:0):특수상태 == 특수상태_방랑
RETURN TALENT:(ARG:0):연모 || TALENT:(ARG:0):복종
SIF CFLAG:(ARG:0):특수상태 == 특수상태_미등장 || CFLAG:(ARG:0):특수상태 == 특수상태_사망
RETURN 0
RETURN CFLAG:(ARG:0):소속 == CFLAG:MASTER:소속 && CFLAG:(ARG:0):포로처 == 0 && !IS_ANIMAL(ARG:0) 

;-------------------------------------------------
;「무기변경」의 오른쪽 컬럼 표시 처리
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW55
CALL COLUMN_RIGHT_TITLE("대상자 선택", "1", "1", "0", "0", "0")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL
CALL COLOR_PRINT(" 학생들의 무기를 변경 할 수 있습니다 ", 칼라_주석)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_CHARALIST
RETURN 0

;-------------------------------------------------
;「무기변경」의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY55
FLAG:거점페이즈페이지 = 1
RETURN 0

;-------------------------------------------------
;「무기변경」의 입력 처리
;-------------------------------------------------
@SHOP_LIFE_USERSHOP55(대상)
#DIM 대상
#DIM FIRST_LINE
$INPUT_LOOP
FIRST_LINE = LINECOUNT
CALL 장비변경슬롯선택(대상)
SETCOLOR 칼라_선택불가
CALL SINGLE_DRAWLINE
RESETCOLOR
PRINTBUTTON @"[%"돌아온다", MAX_CHARANAME_LENGTH, LEFT%]", 999
PRINTL
INPUT
IF RESULT == 999
ELSEIF RESULT <= 2
CALL 장비변경종류선택(대상,RESULT)
GOTO INPUT_LOOP
ELSE
CLEARLINE LINECOUNT - FIRST_LINE
GOTO INPUT_LOOP
ENDIF
