;-------------------------------------------------
;「투기장」의 본체
;-------------------------------------------------
@SHOP_LIFE_ARENA(대상)
#DIM 대상
#DIM 적전투력
#DIM 상대남
#DIMS CONST 상대후보남 = "늑대 같은 남자", "텐구", "오니", "들요괴", "불량배"
#DIMS CONST 상대후보녀 = "텐구아가씨", "갓파아가씨", "여용병", "무녀", "도둑아가씨"
#DIMS 상대이름
#DIM 출장캐릭터
#DIM 대전상대가캐릭터
; ------------몹 이름이랑 스탠딩 사진도 바꾸고 싶은데 바꿀 줄 몰라서 못바꿈---------------
DUMPRAND

출장캐릭터 = -1
CALL SELECT_CHARA_RANDOM("ARENA")
IF RESULT != -1
	IF (IS_MALE(대상) && IS_FEMALE(RESULT))
	ELSEIF (HAS_PENIS(대상) && IS_FEMALE(RESULT))
	ELSEIF (IS_FEMALE(대상) && IS_MALE(RESULT))
	ELSE
		GOTO ENEMY_CANCEL
	ENDIF
	출장캐릭터 = RESULT
ENDIF

$ENEMY_CANCEL

CALL SINGLE_DRAWLINE
PRINTFORML %조사처리(ANAME(대상),"는")% 지하 투기장에 왔다.
PRINTFORML 많은 사람들과 헤일로를 가진 학생들이 흥분하며 관람하고 있다.

IF !RAND:5
	SELECTCASE RAND:4
		CASE 0
			PRINTFORML 중앙의 링에는 패배한 여학생이 수없이 능욕당하고 있다…
		CASE 1
			PRINTFORML 링 안을 들여다보니 노예로 보이는 여자가 괴물에게 덮쳐져 범해지려 하고 있었다…
		CASE 2
			PRINTFORML 내기에 너무 열중한 관객들이 링 옆에서 장외 난투를 하고 있는 모습이 눈에 들어왔다…
		CASE 3
			PRINTFORML 피투성이가 된 검투사가 들것에 실려 %ANAME(대상)%의 옆을 지나 옮겨졌다…
	ENDSELECT

		PRINTFORML 「잘 왔군, 이곳은 투기장이다.」
		PRINTFORML 「방금 본 대로 이곳은 관객들에게 뜨거운 투쟁을 보여주는 장소다.」
		PRINTFORML 「그것도 평범한 투기장들과 달리, 여자 투사들의 투쟁을.」
		PRINTFORML 「그 의미는… 알겠지?」
		IF IS_MALE(대상)
			PRINTFORML 그는 천박한 미소를 띄웠다.
		ELSE
			PRINTFORML 그는 %ANAME(대상)%의 신체를 핥는 것처럼 응시하며 천박한 미소를 띄웠다.
		ENDIF
	PRINTFORML 「뭐, 이기면 돈을 받고 돌아갈 뿐이다.」
ENDIF

PRINTFORML 
PRINTFORML 「…그럼, 어떤 녀석과 싸우겠나? 강한 놈과 붙는 쪽이 돈을 더 벌 수 있다만.」
PRINTFORML 「참가료는 금 3000이다.」

IF 출장캐릭터 >= 0
	PRINTFORML 「……그러고 보니, 오늘은 특별 참가자가 있었지.」
	PRINTFORML
	PRINTFORML 이쪽의 실력:{ARENA_GET_POWER(대상)}
	CALL ASK_MULTI("약한 적(실력:80)" ,"보통의 적(실력:130)" ,"강한 적(실력:180)", "타박타박(실력:250)", "돌아간다", @"%ANAME(출장캐릭터)%(실력:{ARENA_GET_POWER(출장캐릭터)})")
ELSE
	PRINTFORML 
	PRINTFORML 이쪽의 실력:{ARENA_GET_POWER(대상)}
	CALL ASK_MULTI("약한 적(실력:80)" ,"보통의 적(실력:130)" ,"강한 적(실력:180)", "타박타박(실력:250)", "돌아간다")
ENDIF

대전상대가캐릭터 = 0

SELECTCASE RESULT
	CASE 0
		적전투력 = 60 + RAND:40
	CASE 1
		적전투력 = 90 + RAND:40
	CASE 2
		적전투력 = 160 + RAND:40
	CASE 3
		적전투력 = 230 + RAND:40
	CASE 4
		INITRAND
		RETURN 0
	CASE 5
		적전투력 = ARENA_GET_POWER(출장캐릭터) * RAND(80, 120) / 100
		대전상대가캐릭터 = 1
ENDSELECT

PRINTFORML 금 3000을 지불했다
MONEY -= 3000

IF !대전상대가캐릭터
	IF IS_FEMALE(대상) && HAS_PENIS(대상)
		PRINTFORML 「그리고, 너의 경우엔 상대의 성별도 선택할 수 있게 해줄까?」
		PRINTFORML 「후후, 어느 쪽이든 재미있는 볼거리가 되겠군.」
		PRINTFORML 어느 쪽을 선택할까?
		CALL ASK_YN("남" ,"여")
		상대남 = !RESULT
	ELSEIF IS_MALE(대상)
		상대남 = 0
	ELSE
		상대남 = 1
	ENDIF

	IF 상대남
		상대이름 = %상대후보남:(RAND:VARSIZE("상대후보남"))%
	ELSE
		상대이름 = %상대후보녀:(RAND:VARSIZE("상대후보녀"))%
	ENDIF
ELSE
	상대이름 = %ANAME(출장캐릭터)%
	상대남 = IS_MALE(출장캐릭터)
ENDIF
;-------------이름 마지막에 받침 붙는 캐릭이 슌, 카린, 슈에링밖에 없음ㅋㅋㅋㅋㅋㅋㅋㅋ
PRINTFORML 「좋아, 그렇다면 이 녀석이 상대다.」
PRINTFORMW 상대 이름은 %조사처리(상대이름,"다")%.
PRINTFORML 「죽지만 않는 선에서 마음껏 싸우라고, 흐흐흐.」
PRINTFORMW %조사처리(ANAME(대상),"는")% 접수처를 빠져 나가, 검투장으로 향했다.

IF !대전상대가캐릭터
	IF 상대남
		SELECTCASE RAND:4
			CASE 0
				PRINTFORML 「헤헤헤, 귀여워해 주지.」
				PRINTFORMW %조사처리(상대이름,"는")% 입맛을 다시고 있다.
			CASE 1
				PRINTFORML 「이 녀석, 상등품이구만. 흐흐.」
				PRINTFORMW %조사처리(상대이름,"는")% 추잡한 미소를 띄우고 있다.
			CASE 2
				PRINTFORML 「이거 꽤 즐길 수 있겠는데, 크흐흐.」
				PRINTFORMW %상대이름%의 사타구니가 부풀어 오른 것이 보인다.
			CASE 3
				PRINTFORML 「젠장, 더는 못참겠군! 빨리 시작해라!」
				PRINTFORMW %조사처리(상대이름,"는")% 핏발이 선 눈으로 이쪽을 보고 있다.
		ENDSELECT
	ELSE
		SELECTCASE RAND:4
			CASE 0
				PRINTFORML 「우습게 보지 마세요!」
				PRINTFORMW %조사처리(상대이름,"는")% 다가올 전투를 준비했다.
			CASE 1
				PRINTFORML 「…절대로 지지 않는다!」
				PRINTFORMW %조사처리(상대이름,"는")% 이쪽을 홱 노려봤다.
			CASE 2
				PRINTFORML 「빨리 쓰러트리고 돌려보내 줄게.」
				PRINTFORMW 하지만 말과 정반대로 희미하게 떨고 있다.
			CASE 3
				PRINTFORML 「%ANAME(대상)%정도로는 역부족이야.」
				PRINTFORMW %조사처리(상대이름,"는")% 겁없는 미소를 띄우고 있다.
		ENDSELECT
	ENDIF
ENDIF

CALL ARENA_BATTLE(대상, 상대이름, 적전투력)

IF RESULT > 0
	CALL ARENA_WIN(대상, 상대이름, 상대남, 대전상대가캐릭터 ? 출장캐릭터 # -1)
	CALL COLOR_PRINTW(@"상금{ARENA_CALC_MONEY(대상, 적전투력)}를 받았다", 칼라_주의)
	MONEY += ARENA_CALC_MONEY(대상, 적전투력)
	CALL PRINT_ADD_EXP(대상, "무투경험치", ARENA_CALC_EXP(대상, 적전투력), 1)
	CALL PRINT_ADD_EXP(대상, "방위경험치", ARENA_CALC_EXP(대상, 적전투력), 1)
	CALL TRAIN_AUTO_ABLUP(대상)
ELSE
	CALL ARENA_LOSE(대상, 상대이름, 상대남, 대전상대가캐릭터 ? 출장캐릭터 # -1)
	CALL PRINT_ADD_EXP(대상, "무투경험치", ARENA_CALC_EXP(대상, 적전투력) / 10, 1)
	CALL PRINT_ADD_EXP(대상, "방위경험치", ARENA_CALC_EXP(대상, 적전투력) / 10, 1)
	CALL TRAIN_AUTO_ABLUP(대상)
ENDIF

CFLAG:대상:행동완료 = 1

INITRAND

RETURN 1


;-------------------------------------------------
;「투기장」의 대전 상대 결정
;-------------------------------------------------
@SELECT_CHARA_RANDOM_LOGIC_ARENA(대상)
#DIM 대상
SIF MASTER == 대상
	RETURN 0
SIF IS_SP_COUNTRY_CHARA(대상)
	RETURN 0
SIF IS_ANIMAL(대상)
	RETURN 0
SIF CFLAG:대상:포로처
	RETURN 0
RETURN 1


;-------------------------------------------------
;「투기장」전투력 산출
;-------------------------------------------------
@ARENA_GET_POWER(대상)
#FUNCTION
#DIM 대상
RETURNF MAX(ABL:대상:무투 + ABL:대상:방위, ABL:대상:무투 + ABL:대상:지략, ABL:대상:방위 + ABL:대상:지략)

;-------------------------------------------------
;「투기장」상금 산출
;-------------------------------------------------
@ARENA_CALC_MONEY(대상, 적전투력)
#FUNCTION
#DIM 대상
#DIM 전투력
#DIM 적전투력
#DIM 실력비
#DIM 상금
전투력 = ARENA_GET_POWER(대상)
실력비 = 적전투력 * 100 / 전투력
상금 = 3000 + 적전투력 * 40 * 실력비 / 100
RETURNF 상금

;-------------------------------------------------
;「투기장」상금 산출
;-------------------------------------------------
@ARENA_CALC_EXP(대상, 적전투력)
#FUNCTION
#DIM 대상
#DIM 전투력
#DIM 적전투력
#DIM 실력비
#DIM 경험치
전투력 = ARENA_GET_POWER(대상)
실력비 = 적전투력 * 100 / 전투력
경험치 = (RAND:20 + 1 + 적전투력 / 10) * 실력비 / 100
RETURNF 경험치

;-------------------------------------------------
;「투기장」전투 파트
;-------------------------------------------------
@ARENA_BATTLE(대상, 상대이름, 적전투력)
#DIM 대상
#DIMS 상대이름
#DIM 적전투력
#DIM 누적결과
#DIM 전투결과

CALL SINGLE_DRAWLINE
PRINTFORMW %조사처리(ANAME(대상),"가")% 준비를 알리자 심판이 결투 시작을 알렸다!
누적결과 = 0
WHILE 1
	전투결과 = RAND(ARENA_GET_POWER(대상) / 2, ARENA_GET_POWER(대상)) - RAND(적전투력 / 2, 적전투력)
	IF 전투결과 > 0
		PRINTFORML %ANAME(대상)%의 공격이 %상대이름%를 잡았다!
		PRINTFORMW %상대이름%에 데미지를 주었다
	ELSE
		PRINTFORML %상대이름%의 공격을 피하지 못했다!
		PRINTFORMW %조사처리(ANAME(대상),"는")% 데미지를 받았다
	ENDIF
	IF 전투결과 > 0
		누적결과  ++
	ELSE
		누적결과 --
	ENDIF
	SIF ABS(누적결과) >= 3
		BREAK
WEND

CALL SINGLE_DRAWLINE
PRINTFORM 「거기까지!」

RETURN 누적결과

;-------------------------------------------------
;「투기장」승리
;-------------------------------------------------
@ARENA_WIN(대상, 상대이름, 상대남, 상대캐릭터)
#DIM 대상
#DIMS 상대이름
#DIM 상대남
#DIM 상대캐릭터

PRINTFORMW %조사처리(ANAME(대상),"는")% 훌륭히 승리했다!
PRINTFORML ·
PRINTFORML ·
PRINTFORMW ·
IF !상대남
	PRINTFORML 힘이 다한 %조사처리(상대이름,"는")% %ANAME(대상)%의 앞에 쓰러져 지면에 푹 엎드려 있다.
	PRINTFORMW 관객들에게서 열렬한 강간 요청이 쏟아졌다.
	PRINTFORML 어떻게 할까?
	CALL ASK_YN("아무것도 하지 않는다" ,"범한다")
	IF RESULT == 0
		PRINTFORML 저것들을 기쁘게 해 줄 생각은 없다.
		PRINTFORMW %조사처리(ANAME(대상),"는")% 그녀를 껴안고 함께 투기장을 빠져나왔다.
	ELSE
		PRINTFORML 이런 찬스를 날릴 생각은 없다.
		PRINTFORMW %조사처리(ANAME(대상),"는")% 씨익 웃으며 그녀에게 다가갔다.
		CALL ARENA_RAPE_MALE(대상, 상대이름, 상대캐릭터)
		IF 상대캐릭터 != -1
			IF GET_COUNTRY_BOSS(CFLAG:상대캐릭터:소속) == 상대캐릭터
				PRINTFORMW %조사처리(ANAME(상대캐릭터,"가"))% 패배해 능욕당했다는 소식은 곧바로 암암리에 키보토스 전체로 퍼져나갔다……
				FOR LOCAL, 0, MAX_COUNTRY
					SIF !IS_COUNTRY(LOCAL) || GROUPMATCH(LOCAL, CFLAG:대상:소속, CFLAG:상대캐릭터:소속)
						CONTINUE
					CALL CHANGE_RELATION_C_TO_C(LOCAL, CFLAG:상대캐릭터:소속, -300, 300)
				NEXT
			ENDIF
			CFLAG:상대캐릭터:호감도 -= 500
			CFLAG:상대캐릭터:종속도 += 500
			CFLAG:상대캐릭터:의존도 += 500
		ENDIF
	ENDIF
ELSE
	PRINTFORML %ANAME(대상)%의 승리에 관객들로부터 야유가 날아들었다.
	PRINTFORML 그리고 쓰러져 있는 %상대이름%에게 욕을 퍼붓기 시작했다.
	PRINTFORMW %조사처리(ANAME(대상),"는")% 소동에 휘말리기 전에 그 자리를 뒤로 했다.
ENDIF
PRINTFORML 
PRINTFORMW %조사처리(ANAME(대상),"는")% 상금을 받고 가벼운 발걸음으로 투기장을 뒤로 했다.

;-------------------------------------------------
;「투기장」패배
;-------------------------------------------------
@ARENA_LOSE(대상, 상대이름, 상대남, 상대캐릭터)
#DIM 대상
#DIMS 상대이름
#DIM 상대남
#DIM 상대캐릭터

PRINTFORMW %조사처리(ANAME(대상),"는")% 당해버리고 말았다…
PRINTFORML ·
PRINTFORML ·
PRINTFORMW ·
IF !상대남
	PRINTFORML 패배한 %ANAME(대상)%에 욕설이 날아들었다.
ELSE
	PRINTFORMW 패배한 %ANAME(대상)%에 욕설이 날아들었다.
	PRINTFORML 힘이 다해 땅바닥에 쓰러진 %ANAME(대상)%에게 %조사처리(상대이름,"가")% 천천히 다가온다.
	PRINTFORMW %조사처리(ANAME(대상),"는")% 어떻게든 피하려고 했지만 그런 저항이 무색하게 상대에게 덮쳐졌다.
	CALL ARENA_RAPE_FEMALE(대상, 상대이름, 상대캐릭터)
	IF GET_COUNTRY_BOSS(CFLAG:대상:소속) == 대상
		PRINTFORMW %조사처리(ANAME(대상),"가")% 패배해 능욕당했다는 소식은 곧바로 암암리에 키보토스 전체로 퍼져나갔다……
		FOR LOCAL, 0, MAX_COUNTRY
			SIF !IS_COUNTRY(LOCAL) || LOCAL == CFLAG:대상:소속
				CONTINUE
			CALL CHANGE_RELATION_C_TO_C(LOCAL, CFLAG:대상:소속, -300, 300)
		NEXT
	ENDIF
ENDIF
CALL ADD_COOLTIME(대상, 2)
PRINTFORML 
PRINTFORMW …%조사처리(ANAME(대상),"는")% 몸을 질질 끌면서 거점으로 돌아왔다.

